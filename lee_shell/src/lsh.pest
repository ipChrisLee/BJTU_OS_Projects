WHITESPACE = _{
    " " | "\t" | "\r"
    // A trailing backslash at the end of a line.
    | "\\\n"
}
COMMENT = _{ "#" ~ (!("\n") ~ ANY)* }
STR_INNER = @{
    (
        !("\"" | "\\" | "`" | "$") ~ ANY
        | "\\" ~ ANY
        | WHITESPACE
    )+
}
D_QUOTED_STR = @{
	"\"" ~ STR_INNER ~ "\""
}
NO_SPACE_STR = @{
	("." | "-" | "_" | ASCII_ALPHANUMERIC)+
}

FILE_NAME_NO_PATH = @{
	("-" | "_" | ASCII_ALPHANUMERIC) ~ ("." | "-" | "_" | ASCII_ALPHANUMERIC)+
}

FILE_NAME_WITH_PATH = @{
	("/" | "./" | "../") ~ (FILE_NAME_NO_PATH ~ "/")* ~ FILE_NAME_NO_PATH
}

FILE_NAME = {
	!("/" | "./" | "../") ~ FILE_NAME_NO_PATH |
	FILE_NAME_WITH_PATH
}

SCRIPT = _{ SOI ~ PIPE_CMD? ~ EOI }
WS = _{ (WHITESPACE | COMMENT)+ }
PIPE_CMD = { CMD ~ ("|" ~ CMD)* }
REDIRECT_IO = {
	&"<" ~ CHANGE_INPUT ~ (&">" ~ CHANGE_OUTPUT)? |
	&">" ~ CHANGE_OUTPUT ~ (&"<" ~ CHANGE_INPUT)?
}
CHANGE_INPUT = {
    "<" ~ FILE_NAME
}
CHANGE_OUTPUT = {
	">" ~ FILE_NAME
}
CMD = { CMD_NAME ~ ARG* ~ REDIRECT_IO? }
CMD_NAME = _{
	BUILTIN_CMD |
	OUTER_CMD |
	PERF_EXE
}
BUILTIN_CMD = {
	"cd" | "exit" | "exec" | "kill" | "export" | "echo" | "pwd"
}
OUTER_CMD = {
	FILE_NAME_NO_PATH
}
PERF_EXE = {
	FILE_NAME_WITH_PATH
}
ARG = _{
	D_QUOTED_STR | NO_SPACE_STR
}
